#------------------------------------------------------------------------------------------------------
#
# boot.ini identification
#
#------------------------------------------------------------------------------------------------------
ODROIDC-UBOOT-CONFIG

# U-Boot Parameters
setenv initrd_high "0xffffffff"
setenv fdt_high "0xffffffff"

#------------------------------------------------------------------------------------------------------
#
# Boot Specific Stuff
#
#------------------------------------------------------------------------------------------------------
setenv bootrootfs "boot=LABEL=userdata disk=LABEL=oelec ro"

#------------------------------------------------------------------------------------------------------
#
# Console Setup
#   setenv condev "console=ttyS0,115200n8"                # on serial port
#   setenv condev "console=tty0"                          # on display (HDMI)
#   setenv condev "console=tty0 console=ttyS0,115200n8"   # on both
#
#------------------------------------------------------------------------------------------------------
setenv console "console=tty0 console=ttyS0,115200n8"

#------------------------------------------------------------------------------------------------------
#
# Video
#   Setup the video output
#     setenv video_output "vga"            # 640x480
#     setenv video_output "480p"           # 720x480
#     setenv video_output "576p"           # 720x576
#     setenv video_output "800x480p60hz"   # 800x480
#     setenv video_output "800x600p60hz"   # 800x600
#     setenv video_output "1024x600p60hz"  # 1024x600
#     setenv video_output "1024x768p60hz"  # 1024x768
#     setenv video_output "1360x768p60hz"  # 1360x768
#     setenv video_output "1366x768p60hz"  # 1366x768
#     setenv video_output "1440x900p60hz"  # 1440x900
#     setenv video_output "1600x900p60hz"  # 1600x900
#     setenv video_output "1680x1050p60hz" # 1680x1050
#     setenv video_output "720p"           # 720p@60Hz 1280x720
#     setenv video_output "800p"           # 1280x800
#     setenv video_output "sxga"           # 1280x1024
#     setenv video_output "1080i50hz"      # 1080I@50Hz
#     setenv video_output "1080p24hz"      # 1080P@24Hz
#     setenv video_output "1080p50hz"      # 1080P@50Hz
#     setenv video_output "1080p"          # 1080P@60Hz 1920x1080
#     setenv video_output "1920x1200"      # 1920x1200
#
#  Setup the video mode
#     setenv video_mode "hdmi"             # HDMI
#     setenv video_mode "dvi"              # DVI
#     setenv video_mode "vga"              # VGA
#
#  Setup the HDMI BPP Mode
#     setenv video_bpp "32"                # 32-bit
#     setenv video_bpp "24"                # 24-bit
#     setenv video_bpp "16"                # 16-bit
#
#  Enable|Disable HDMI Hotplug Detection
#     setenv hdmihotplug "0"               # Disabled
#     setenv hdmihotplug "1"               # Enabled
#
#  Enable|Disable HDMI (Saves RAM)
#     setenv hdmioutput "0"                # Disabled
#     setenv hdmioutput "1"                # Enabled
#
#  Enable|Disable VPU (Saves RAM)
#     setenv vpu "0"                       # Disabled
#     setenv vpu "1"                       # Enabled
#
#------------------------------------------------------------------------------------------------------
setenv video_output "1080p"
setenv video_mode   "hdmi"
setenv video_bpp    "32"
setenv hdmihotplug  "0"
setenv hdmioutput   "1"
setenv vpu          "1"

#------------------------------------------------------------------------------------------------------
#
# OpenELEC variables
#
# Setup the OpenELEC options
#   valid values are: textmode debugging progress nofsck nosplash noram overlay quiet ssh
#------------------------------------------------------------------------------------------------------
setenv openelec "quiet ssh"

#------------------------------------------------------------------------------------------------------
#
# UHS Card Configuration
#   Uncomment the line below to __DISABLE__ UHS-1 MicroSD support
#   This might break boot for some brand models of cards.
#
#------------------------------------------------------------------------------------------------------
# setenv disableuhs "disableuhs"

#------------------------------------------------------------------------------------------------------
#
# Enable|Disable HDMI CEC Control
#   setenv hdmi_cec  "0"                  # Disabled
#   setenv hdmi_cec  "1"                  # Enabled
#
#------------------------------------------------------------------------------------------------------
setenv hdmi_cec "0"

#------------------------------------------------------------------------------------------------------
#
# PCM5102 I2S Audio DAC (HiFi Shield)
# PCM5102 is an I2S Audio DAC Addon board for ODROID-C1+
#
# Enable|Disable Audio DAC
#   setenv audio_dac "0"                  # Disabled
#   setenv audio_dac "1"                  # Enabled
#------------------------------------------------------------------------------------------------------
setenv audio_dac "0"

#------------------------------------------------------------------------------------------------------
#
# Odroid C1 specific
#
#------------------------------------------------------------------------------------------------------
setenv odroidp1 "no_console_suspend vdaccfg=0xa000 logo=osd1,loaded,0x7900000,720p,full"
setenv odroidp2 "dmfc=3 cvbsmode=576cvbs video_output=${video_output} video_bpp=${video_bpp}"
setenv odroidp3 "video_mode=${video_mode} ${disableuhs} consoleblank=0"
setenv odroid   "${odroidp1} ${odroidp2} ${odroidp3}"

#------------------------------------------------------------------------------------------------------
#
# U-Boot bootcmd command
#  
#------------------------------------------------------------------------------------------------------
setenv kernel  "fatload mmc 0:1 21000000 multiboot/KERNEL"
setenv initrd  "fatload mmc 0:1 22000000 multiboot/INITRD"
setenv dtb     "fatload mmc 0:1 21800000 multiboot/meson8b_odroidc.dtb.oelec"
setenv bootseq "bootm 21000000 22000000 21800000"
setenv bootcmd "${kernel}; ${initrd}; ${dtb}; ${bootseq}"

#------------------------------------------------------------------------------------------------------
#
# Kernel boot
#
#------------------------------------------------------------------------------------------------------

# Prepare to boot
if test "${hdmi_cec}" = "1"; then setenv cec "hdmitx=cecf"; fi
if test "${hdmi_hotplug}" = "0"; then setenv hpd "disablehpd=true"; fi
if test "${audio_dac}" = "1"; then setenv dac "enabledac"; fi
if test "${vpu}" = "0"; then fdt rm /mesonstream; fdt rm /vdec; fdt rm /ppmgr; fi
if test "${hdmioutput}" = "0"; then fdt rm /mesonfb; fi

# Populate the final bootcmd
setenv bootargs "${console} ${bootrootfs} ${odroid} ${cec} ${hpd} ${dac} ${openelec}"

# =================================================
# = RESTORE boot.ini FOR MULTIBOOT, DO NOT CHANGE =
# =================================================
fatload mmc 0:1 0x30000000 multiboot/boot.ini.multi
fatwrite mmc 0:1 0x30000000 boot.ini ${filesize}
# =================================================

# Boot the board
run bootcmd
